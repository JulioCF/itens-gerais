//===== Cronus Script ================================================= 
//= Mercenário Shifty
//===== Por: ==========================================================
//= acky - god@acky.com
//===== Versão Atual: ================================================= 
//= 1.3a CR / 1.0 eA
//===== Descrição: ==================================================== 
//= Jogadores compram Ninjas para assassinar outros jogadores
//===== Changelog: ==================================================== 
//= 1.0 Script incicial. [acky]
//= 1.1 Traduzido. [Mehah]
//= 1.1a Corrigido algumas traduções. [Mehah]
//= 1.2 Otimizado e algumas correções. [RoM]
//= 1.3 Removido a variável do npc_athena.conf (ele não existe mais). [RoM]
//= 1.3a Removido show e digit [Keoy]
//=====================================================================

morocc,148,86,5	script	Mercenário Shifty	725,{

//========== Configuração: ==========
// Custo para cada ninja:
set .@custo,250000;
//===================================

	mes "[Mercenário Shifty]";
	mes "O que você deseja?";
	set .@loop,1;
	while(.@loop){
		next;
		if(getgmlevel() > 90) set .@menu$,select("Comprar Ninjas","Assassinar alguém","Verificar seus Ninjas","Adicionar Ninjas","Cancelar");
		else set .@menu$,select("Comprar Ninjas","Assassinar alguém","Verificar seus Ninjas","","Cancelar");
		switch(.@menu$){

			Case 1:
			mes "[Mercenário Shifty]";
			mes "Quantos ninjas você deseja comprar?";
			mes "Há ^0000FF" + $ninja_avail + "^000000 ninjas disponíveis.";
			mes "Custa ^0000FF" + .@custo + " zeny ^000000cada um.";
			input @buy;
			next;
			mes "[Mercenário Shifty]";
			if($ninja_avail < 1){
				mes "Não há nenhum ninja disponível para comprar.";
				close;
				}
			if($ninja_avail < @buy){
				mes "A quantidade de ninjas que você quer não está disponível.";
				break;
				}
			set @price,@buy*.@custo;
			if(Zeny < @price){
				mes "Você não tem o zeny necessário.";
				close;
				}
			mes "Isso custará a você ^0000FF"+@price+" zeny^000000.";
			next;
			if(select("Continuar","Cancelar") == 2) close;
			set Zeny,Zeny-@price;
			set #ninjas,#ninjas+@buy;
			set $ninja_avail,$ninja_avail-@buy;
			mes "[Mercenário Shifty]";
			mes "Obrigado.";
			close;

			Case 2:
			mes "[Mercenário Shifty]";
			if(agitcheck()){
				mes "Desculpe, todos meus ninjas estão ocupados na Guerra de Emperium.";
				close;
				}
			mes "Ponha o nome do alvo.";
			mes "^FF0000Datilografe o nome exatamente, se não, eu não poderei encontrar a vítima.^000000";
			next;
			if(select("Continuar","Cancelar") == 2) close;
			input @name$;
			mes "[Mercenário Shifty]";
			mes "Ninjas Ativos: "+#ninjas;
			mes "Ninjas Descansando: "+#ninjasr;
			mes "Quantos você quer emitir?";
			input @number;
			next;
			mes "[Mercenário Shifty]";
			if (@number < 1){
				mes "Você não pode emitir nenhum ninja ou quantidade negativa.";
				break;
				}
			if (@number > #ninjas){
				mes "Você não tem esta quantidade de ninjas.";
				break;
				}
			if (@number > 10){
				mes "Você só pode emitir 10 ninjas no máximo.";
				break;
				}
			set @chance,rand(1,12);
			set #ninjas,#ninjas-@number;
			set #ninjas,#ninjas+#ninjasr;
			set #ninjasr,0;
			mes "Emitindo ninjas agora.";
			next;
			mes "[Mercenário Shifty]";
			if(@number < @chance){
				set @ninjasurvived,rand (1,@number);
				set #ninjasr,@number-@ninjasurvived;
				mes "Seu ataque falhou e somente ^FF0000"+#ninjasr+"^000000 Ninjas sobreviveram.";
				announce @name$+" sobreviveu os ataque dos Ninjas de "+strcharinfo(0)+".",8;
				close;
				}
			set @ninjasurvived,rand (1,@number);
			set #ninjasr,@number-@ninjasurvived;
			mes "Seu ataque foi bem sucedido, mas somente ^FF0000"+#ninjasr+"^000000 Ninjas sobreviveram.";
			atcommand strcharinfo(0) + "@kill "+@name$;
			announce @name$+" foi assassinado pelos Ninjas de "+strcharinfo(0)+".",8;
			close;

			Case 3:
			mes "[Mercenário Shifty]";
			mes "Você tem:";
			mes "^FF0000" + #ninjas + "^000000 Ninjas Ativos.";
			mes "^0000FF" + #ninjasr + "^000000 Ninjas Descansando.";
			break;

			Case 4:
			mes "[Mercenário Shifty]";
			mes "Quantos ninjas disponíveis você deseja adicionar?";
			next;
			input @add;
			set $ninja_avail,$ninja_avail+@add;
			mes "[Mercenário Shifty]";
			mes ""+@add+" ninjas adicionados. Total de "+$ninja_avail+" disponíveis.";
			close;

			Case 5:
			close;
			}
		}

OnInit:
	set $ninja_avail,$ninja_avail+1;
	end;

OnMinute00:
	if(gettime(3)%2 == 1 && gettime(3) > 10) set $ninja_avail,$ninja_avail+2;
	end;

}