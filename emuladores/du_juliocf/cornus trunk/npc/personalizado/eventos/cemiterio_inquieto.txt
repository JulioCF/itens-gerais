//===== Cronus Script ======================================= 
//= Quest (Cemitério Inquieto) de Prontera
//===== Por: ================================================== 
//= Lupus
//===== Versão Atual: ===================================== 
//= 1.2a CR / 1.0 eA
//===== Comentarios Adicionais: ================================= 
//= 1.0 Script inicial. [Lupus]
//= 1.0a Traduzido. [Kindred]
//= 1.0b Corrigido o erro do additem. [Kamper]
//= 1.2 Otimizado e algumas correções. [RoM]
//= 1.2a strsex removido [Keoy]
//============================================================

prontera,3,3,3	script	Uneasy_Check	-1,{
	end;

OnHour00:
	set $UNEASY_DL,$UNEASY_DL-1;
	set $UNEASY_BL,$UNEASY_BL+30;
	if($UNEASY_BL > 666) set $UNEASY_BL,666;
	if($UNEASY_DL < 0) {
		killmonsterall "prontera";
		enablenpc "Madre Mathana";
		set $@UNEASY_MOB,65;
		monster "prontera",0,0,"Zumbi",1015,30,"Uneasy_Check::OnZombieDead";
		monster "prontera",268,349,"Zumbi",1015,30,"Uneasy_Check::OnZombieDead";
		monster "prontera",269,350,"Carniçal",1036,5,"Uneasy_Check::OnZombieDead";
		mapannounce "prontera","[Madre Mathana]: O cemitério acordou! Em nome de Odin! Corram para o santuário! Salvem Prontera!",0;
		end;
		}
	disablenpc "Madre Mathana";
	end;

OnInit:
	if ($UNEASY_DL >= 0) disablenpc "Madre Mathana";
	end;

OnHour06:
	killmonsterall "prontera";
	end;

OnHour01:
	if($@UNEASY_MOB > 0) mapannounce "prontera","[Madre Mathana]: Em nome de Odin! Acabe com essas sobras de mortos!!!",0;
	end;

OnZombieDead:
	set $@UNEASY_MOB,$@UNEASY_MOB-1;
	if($@UNEASY_MOB) end;
	set $UNEASY_DL,0;
	set $UNEASY_H$,strcharinfo(0);
	mapannounce "prontera","[Madre Mathana]: "+(Sex?"O Bravo":"A Guerreira")+" "+strcharinfo(0)+" acabou de matar o último zumbi de Prontera!",0;
	getexp 50,100;
	end;
}

prontera,257,313,5	script	Madre Mathana	79,{
	show "[Madre Mathana]";
	if($UNEASY_DL <= 0){
		if(!$UNEASY_DL){
			show "ELES podem retornar novamente amanhã!";
			if ($UNEASY_H$ == strcharinfo(0)) show "Obrigado, "+strcharinfo(0)+"! Agora nós poderemos dormir até a meia-noite de amanhã!";
			else show "Ainda bem que "+$UNEASY_H$+" nos ajudou, agora poderemos dormir em paz!";
			}
		show "Para acalmar a fúria desse cemitério nós devemos jogar Água Benta em todas as covas, mas está faltando Água Benta.";
		show "Você pode nos ajudar dando mais?";
		next;
		if(!countitem(523)) set .@menu$,select("Não tenho nenhuma","Cancelar");
		else set .@menu$,select("Sim, tome toda a minha Água Benta!","Cancelar");
		switch(.@menu$){
		Case 1:
			show "[Madre Mathana]";
			if(!countitem(523)){
				show "Ainda precisamos de mais "+$UNEASY_BL+" garrafas... Porque você não vai procurar mais garrafas com outras pessoas?";
				show "O velho cemitério estará fora de controle em breve...";
				show "Por favor, em nome de Odin, ajude a cidade de Prontera.";
				close;			
				}
			set $UNEASY_BL,$UNEASY_BL-countitem(523);
			delitem 523,countitem(523);
			if($UNEASY_BL > 0){
				show "Obrigado "+strcharinfo(0)+", mas ainda precisamos de mais "+$UNEASY_BL+" garrafas de Água Benta.";
				close;
				}
			set $UNEASY_DL,5+((0-$UNEASY_BL)/30);
			set $UNEASY_H$,strcharinfo(0);
			show "Obrigado, "+strcharinfo(0)+"! Agora temos Água benta suficiente!";
			show "No momento que encharcarmos o cemitério com Água benta teremos "+$UNEASY_DL+" noites calmas!";
			next;
			killmonsterall "prontera";
			show "[Madre Mathana]";
			show "Viu, "+strcharinfo(0)+"? Eles já foram embora!";
			show "A nossa igreja irá agradecê-lo pessoalmente...";
			next;
			mapannounce "prontera","[Madre Mathana]: Em nome de Odin declaro "+(Sex?"o Bravo":"a Bela")+" "+strcharinfo(0)+" como "+(Sex?"o salvador":"a salvadora")+" de prontera!",0;
			show "[Madre Mathana]";
			show "Em nome de Odin, eu te abençôo e te presenteio com este modesto Presente de Mareusis.";
			additem 505,1;
			getexp 50,100;
			close;

		Case 2:
			close;
		}
		}
	show "Sinto que há algo de errado em nosso cemitério";
	if ($UNEASY_H$ == strcharinfo(0)) show "Mas graças à você, "+strcharinfo(0)+", nós poderemos dormir novamente mais "+$UNEASY_DL+" noites calmas!";
	else show "Mas graças à ajuda de "+$UNEASY_H$+" nós teremos mais "+$UNEASY_DL+" noites calmas!";
	emotion 0;
	close;
}