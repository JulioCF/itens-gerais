//===== Cronus Script ========================================
//= Evento de Natal
//===== Por ==================================================
//= Banned
//===== Versão ===============================================
//= 1.5a
//===== Descrição ============================================
//= Sumona Monstros Natalinos.
//===== Changelog ============================================
//= 1.0 Script inicial. [Banned]
//= 1.1 Adicionado set de mapflags. [Banned]
//= 1.2 Reformulado, coisas inúteis retiradas. [Banned]
//= 1.3 Corrigida contagem de pontos. [Banned]
//= 1.4 Script Reestruturado. [Mehah]
//= 1.5 Otimizado e algumas correções. [RoM]
//= 1.5a Pequenas correções [Keoy]
//			Também deixei o npc mais "amigável"
//============================================================
prontera,142,174,5	script	Evento de Natal::Evento de Natal	718,{

	mes "[Papai Noel]";
	mes "^6495ED~Ho ho ho^000000";
	next;
	mes "[Papai Noel]";
	mes "Olá "+strcharinfo(0)+"!";
	mes "O que você gostaria de fazer?";
	next;
	if(getgmlevel() >= 80) set .@m$,"Manipular o Evento";
	while(1)
	{
		switch(select(.@m$,"Contar Meus Pontos","Pegar meu Prêmio!","Nada, obrigad"+(Sex)? "o":"a"+"."))
		{
			mes "[Papai Noel]";

			case 1:
			mes "O que gostaria de fazer com o Evento?";
			next;
			switch(select("Vamos Começar!","Parar o Evento!","Cancelar"))
			{
				mes "[Papai Noel]";

				case 1:
				mes "Evento iniciado!";
				close2;
				set $xmasMobs,250;
				monster "prontera",0,0,"Antônio",1247,50,"Evento de Natal::OnMobDead";
				monster "prontera",0,0,"Cookie Natalino",1246,50,"Evento de Natal::OnMobDead";
				monster "prontera",0,0,"Poring Noel",1062,50,"Evento de Natal::OnMobDead";
				monster "prontera",0,0,"Presente",1249,50,"Evento de Natal::OnMobDead";
				monster "prontera",0,0,"Goblin Natalino",1245,50,"Evento de Natal::OnMobDead";
				announce "[Papai Noel] Ho ho ho~ Parece-me que alguns monstros invadiram Prontera",8;
				sleep2 1000;
				announce "[Papai Noel] Quem será que poderá ajudar os habitantes da cidade?",8;
				sleep2 1000;
				announce "[Papai Noel] Grandes recompensas serão dadas aos aventureiros ajudarem!",8;
				sleep2 1000;
				announce "[Papai Noel] Feliz natal ^^",8;
				initnpctimer;
				end;

				case 2:
				mes "Evento finalizado!";
				close2;
				//announce "O GM "+strcharinfo(0)+" finalizou o Evento de Natal!",8;
				announce "[Papai Noel] O evento foi finalizado por um GameMaster :(",8;
				sleep 500;
				announce "[Papai Noel] Mesmo assim temos presentes para todos que ajudaram matando mais de 10 monstros...",8;
				stopnpctimer;
				set $xmasMobs,0;
				killmonsterall "prontera";
				sleep 500;
				announce "[Papai Noel] Feliz Natal!",8;
				end;

				case 3:
				mes "Feliz natal";
				close;
				}

			case 2:
			mes "Você tem "+xmasEventKill+" pontos!";
			next;
			break;

			case 3:
			mes "Escolha seu presente!";
			next;
			switch(select("1 Velha Caixa Azul [^00FF0070 Pontos^000000]",
			"1 Caixa de Presente [^00FF0050 Pontos^000000]",
			"1 Gorro de Natal [^00FF0025 Pontos^000000]",
			"50 Bengala Doce [^00FF0020 Pontos^000000]",
			"75 Biscoito de Gengibre [^00FF0015 Pontos^000000]",
			"100 Doce da Sorte [^00FF0010 Pontos^000000]",
			"Nenhum")){
				case 1: callsub mobKillCheck,70,603,1;
				case 2: callsub mobKillCheck,50,644,1;
				case 3: callsub mobKillCheck,25,2236,1;
				case 4: callsub mobKillCheck,20,530,50;
				case 5: callsub mobKillCheck,15,538,75;						
				case 6: callsub mobKillCheck,10,570,100;						
				}

			case 4:
			mes "Feliz Natal ^^";
			close;
		}
	}

mobKillCheck:
	mes "[Papai Noel]";
	if(xmasEventKill < getarg(0)) mes "Você não tem pontos suficientes :(.";
	else {
	mes "Muito Bem, aí está seu prêmio!";
	getitem getarg(1),getarg(2);
	set xmasEventKill,xmasEventKill-getarg(0);
	}
	close;

OnMobDead:
	set xmasEventKill,xmasEventKill+1;
	set $xmasMobs,$xmasMobs-1;
	if(!$xmasMobs)
	{
		stopnpctimer;
		announce "[Papai Noel] Todos os Monstros foram Mortos!", bc_blue|bc_all;
		sleep 500;
		announce "[Papai Noel] Obrigado pela ajuda de todos, um feliz natal! ~Ho ho ho", bc_blue|bc_all;
		sleep 500;
		announce "[Papai Noel] Quem conseguiu matar mais de 10 monstros pode vir me encontrar que tenho presentes :D", bc_blue|bc_all;
	}
	end;

OnTimer2000:
	announce "[Papai Noel]: Vocês tem 3 minutos para matar todos os monstros!",bc_blue|bc_all;
	end;
OnTimer62000:
	announce "[Papai Noel]: Vocês tem 2 minutos para matar todos os monstros!",bc_blue|bc_all;
	end;
OnTimer92000:
	announce "[Papai Noel]: Matandos os Monstros você ganha pontos e pode trocar por prêmios!",bc_blue|bc_all;
	end;
OnTimer122000:
	announce "[Papai Noel]: Vocês tem 1  minutos para matar todos os monstros!",bc_blue|bc_all;
	end;
OnTimer152000:
	announce "[Papai Noel]: Vocês só tem 30 segundos para matar todos os monstros!",bc_blue|bc_all;
	end;
OnTimer172000:
	announce "[Papai Noel]: Vocês só tem 10 segundos para matar todos os monstros!",bc_blue|bc_all;
	end;
OnTimer182000:
	announce "[Papai Noel]: O Tempo do Evento acabou, vá trocar seus prêmios!",bc_blue|bc_all;
	killmonsterall "prontera";
	stopnpctimer;
	end;
}