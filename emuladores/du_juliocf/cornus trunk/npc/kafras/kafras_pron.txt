//===== Cronus Script ========================================
//= Kafras de Prontera
//===== Por: =================================================
//= eAthena Dev Team
//===== Versão Atual: ========================================
//= 2.7b
//===== Compatibilidade: =====================================
//= Cronus SVN
//===== Descrição: ===========================================
//= Kafras da cidade de Prontera
//===== Comentários Adicionais: ==============================
//= v1.1 Now using functions :)
//= v2.1b Minor changes to function calls. Using arguments.
//= This version uses arrays .[kobra_k88]
//= 2.2 North Kafra allows you save your position
//= 2.3 New Teleport list, new prices, 2.3a fixed Alberta Teleport [Lupus]
//= 2.3b One Kafra's been moved to Prontera, thx to Vicious_Pucca [Lupus]
//= 2.4 Updated Kafras locations for the mini-map [Lupus]
//= 2.41 New prices [Evera]
//= 2.5 Can't save outside city [Evera]
//= 2.6 Fixed central Kafra position (146,86 -> 146,98) [L0ne_W0lf]
//= 2.7 Updated function calls with new arguments. [L0ne_W0lf]
//=	Added unique Kafra Welcome messages.
//= 2.7a Traduzido. [Puncker]
//= 2.7b Revisado. [RoM]
//============================================================

// Norte ==================================
prontera,152,326,3	script	Funcionária Kafra::kaf_prontera	112,{
	cutin "kafra_06",2;
	callfunc "F_KafSetPront";
	mes "[Funcionária Kafra]";
	mes "Bem-vindo à";
	mes "Corporação Kafra~";
	mes "Os Serviços Kafra estão";
	mes "sempre aqui para ajudá-lo.";
	mes "Então, como posso";
	mes "ajudá-lo hoje?";
	callfunc "F_Kafra",5,0,0,40,800;

M_Save:
	savepoint "prontera",157,327;
	callfunc "F_KafEnd",0,1,"na cidade de Prontera";
}

// Sul ====================================
prontera,151,29,0	script	Funcionária Kafra::kaf_prontera2	115,{
	cutin "kafra_03",2;
	callfunc "F_KafSetPront";
	mes "[Funcionária Kafra]";
	mes "Bem-vindo~!";
	mes "Os Serviços Kafra";
	mes "estão sempre ao seu lado.";
	mes "Então, como posso ajudá-lo?";
	callfunc "F_Kafra",5,0,0,40,800;

	M_Save:
		savepoint "prontera",150,33;
		callfunc "F_KafEnd",0,1,"na cidade de Prontera";
}

// Oeste ===================================
prontera,29,207,6	script	Funcionária Kafra::kaf_prontera3	113,{
	cutin "kafra_05",2;
	callfunc "F_KafSetPront";
	mes "[Funcionária Kafra]";
	mes "A Corporação Kafra";
	mes "está sempre trabalhando para";
	mes "lhe dar os melhores serviços.";
	mes "Como posso ajudá-lo?";
	callfunc "F_Kafra",5,0,0,40,800;

	M_Save:
		savepoint "prontera",33,208;
		callfunc "F_KafEnd",0,1,"na cidade de Prontera";
}

// Leste ===================================
prontera,282,200,2	script	Funcionária Kafra::kaf_prontera4	114,{
	cutin "kafra_04",2;
	callfunc "F_KafSetPront";
	mes "[Funcionária Kafra]";
	mes "Bem-vindo!";
	mes "A Corporação Kafra";
	mes "sempre ajudará os";
	mes "aventureiros de Rune-Midgard";
	mes "com seu excelente serviço. Então,";
	mes "o que posso fazer por você hoje?";
	callfunc "F_Kafra",5,0,0,40,800;

	M_Save:
		savepoint "prontera",281,203;
		callfunc "F_KafEnd",0,1,"na cidade de Prontera";
}

// Centro ===================================
prontera,146,89,6	script	Funcionária Kafra::kaf_prontera5	117,{
	cutin "kafra_01",2;
	callfunc "F_KafSetPront";
	mes "[Funcionária Kafra]";
	mes "Bem-vindo à";
	mes "Corporação Kafra.";
	mes "Os Serviços Kafra";
	mes "estão sempre ao seu lado.";
	mes "Como posso ajudá-lo?";
	callfunc "F_Kafra",5,0,0,40,800;

M_Save:
	savepoint "prontera",116,73;
	callfunc "F_KafEnd",0,1,"na cidade de Prontera";
}


// Funcão: Setar as variáveis ----------------------------------------------
function	script	F_KafSetPront	{
	cleararray @wrpC$[0],"",getarraysize(@wrpC$);
	setarray @wrpD$[0], "Izlude", "Geffen", "Payon", "Morroc", "Caverna dos Orcs", "Alberta";
	setarray @wrpP[0], 600, 1200, 1200, 1200, 1200, 1800;
	set @wrpC$[0], @wrpD$[0]+" -> "+@wrpP[0];
	set @wrpC$[1], @wrpD$[1]+" -> "+@wrpP[1];
	set @wrpC$[2], @wrpD$[2]+" -> "+@wrpP[2];
	set @wrpC$[3], @wrpD$[3]+" -> "+@wrpP[3];
	set @wrpC$[4], @wrpD$[4]+" -> "+@wrpP[4];
	set @wrpC$[5], @wrpD$[5]+" -> "+@wrpP[5];
	set @wrpC$[6], "Cancelar";
	setarray @viewpX[0], 151, 29, 282, 152;
	setarray @viewpY[0], 29, 207, 200, 326;
	return;
}