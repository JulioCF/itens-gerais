//===== Cronus Script ======================================== 
//= Script Alquimista
//===== Por: =================================================
//= nestor_zulueta (Fusion)
//= converted by Darkchild
//===== Versão Atual: ======================================== 
//= 1.7a/2.4a
//===== Descrição: =========================================== 
//= Muda a classe para Alquimista
//===== Changelog: =========================================== 
//= 1.4 Traduzido [Kosonome]
//= 1.5 Quest Atualizada [Mehah]
//= 1.6 Adaptado Classname Na Função F_BlockHigh [Mehah]
//= 1.6a Corrigido o recebimentos dos manuáis [Kamper]
//= 1.7 Corrigido respostas da quest de Nicholas Flamel [Page]
//= 1.7a Removido show, additem, digit	[Yum]
//============================================================

alde_alche,27,185,4	script	Parmry Gianino	744,{
	callfunc "F_BlockHigh",Job_Merchant_High,"Mercador T.",Job_Creator,"Criador","Parmry Gianino";

	mes "[Parmry Gianino]";
	if(BaseJob == Job_Merchant) goto L_Merc;
	if(baseJob == Job_Alchem){
		mes "Olá companheiro Alquimista.  Como os negócios estão progredindo?  Espero que bem, boa sorte para você.";
		close;
	}
	mes "Bem vindo a União Alquimista.  Ah, minhas sinceras desculpas mas estou ocupado no momento.";
	close;
L_Merc:	
	if(ALCH_Q == 1) goto L_Check;
	if(ALCH_Q == 2) goto L_Test2;
	if(ALCH_Q == 3) goto L_Test3;
	if(ALCH_Q == 4) goto L_Test4;
	if(ALCH_Q == 5) goto L_Test5;
	if(ALCH_Q == 6) goto L_Test6;
	if(ALCH_Q == 7) goto L_GoChange;
	mes "Bem vindo a União Alquimista.";
	mes "Como eu poderia ajudá-lo?";
	next;
	menu "Eu gostaria de aprender sobre alquímia.",-, "Eu quero virar um Alquimista.",L_Start, "Nada.",M_End;

		mes "[Parmry Gianino]";
		mes "Alquimistas estudam e criam novos materiais usando uma variedade de substâncias existentes.";
		mes "Eles pesquisam sobre os átomos químicos, e experimentam para mudar suas propriedades químicas.";
		next;
		mes "[Parmry Gianino]";
		mes "A maioria das pessoas pensam que o objetivo final é criar ouro, mas esta não é a pura verdade.";
		mes "Eles podem fazer qualquer coisa, de simples remédios até novos materiais.";
		next;
		mes "[Parmry Gianino]";
		mes "As vezes, até mesmo pesquisas sobre a criação da vida... mas este poderia ser considerado um território de Deus...";
		mes "Este é uma área muito complexa, todos somente pesquisam por enquanto.";
		next;
		mes "[Parmry Gianino]";
		mes "Se você está interresado em virar um Alquimista, eu recomendo você ter muitas experiências adquiridas enquanto Mercador.";
		mes "Pois sendo um Mercador é uma grande oportunidade para aprender sobre os materiais enquanto você negocia eles.";
		next;
		mes "[Parmry Gianino]";
		mes "Somente você pode decidir seu caminho.";
		mes "A estrada para se tornar um Alquimista é bem desafiadora.";
		mes "Você precisará se enfocar mais em experimentos e pesquisas do que seus negócios.";
		close;
	M_End:
		mes "[Parmry Gianino]";
		mes "Hmmm... Por favor, quando precisar de algo, somente peça.";
		close;
L_Start:
	mes "[Parmry Gianino]";
	if(JobLevel < 40) goto L_LowLvl;
	mes "Estão era isso. Meu nome é Parmry Gianino da Associação dos Alquimistas.";
	mes "Prazer em conhecer você.";
	next;
	mes "[Parmry Gianino]";
	mes "Se você se juntar a nossa União e passar nos treinamentos você será oficial reconhecido";
	mes "como um Alquimista e apto a ajudar em nossas pesquisas.";
	next;
	mes "[Parmry Gianino]";
	mes "Porém nós não aceitamos qualquer um.";
	mes "Você deve ser dedicado, perseverante, e estar disposto a devotar a si mesmo as pesquisas.";
	next;
	mes "[Parmry Gianino]";
	mes "Há alguns requerimentos para você poder se juntar a associação...";
	mes "porém discutiremos isto assim que você se inscrever.";
	next;
	mes "[Parmry Gianino]";
	mes "Então, você gostaria de se inscrever?";
	next;
	menu "Eu gostaria de me inscrever.",M_Apply,"Eu farei mais tarde.",-;

		mes "[Parmry Gianino]";
		mes "Se você é um Mercador talentoso,";
		mes "você sempre será bem vindo aqui.";
		mes "Volte sempre.";
		close;
	M_Apply:
		mes "[Parmry Gianino]";
		mes "Preencha este formulário por favor...";
		next;
		mes "*você preenche o formulário e o entrega de volta*";
		next;
		mes "[Parmry Gianino]";
		mes "Muito bom.  Para você poder se juntar a União Alquimista você deve primeiramente pagar uma taxa de  ^5533FF50,000^000000 zeny.";
		mes "Você precisará também nos trazer alguns poucos itens.";
		next;
		mes "[Parmry Gianino]";
		mes "Claro que se você nos trazer um ^5533FFGrimório Antigo^000000 e um ^5533FFMartelo de Ferreiro^000000";
		mes "você não terá que pagar a taxa.";
		next;
		mes "[Parmry Gianino]";
		mes "Vamos ver... você precisará nos trazer ^5533FF100 Mini-Fornalhas^000000.";
		mes "Quando você conseguir as Mini-Fornalhas, venha me ver novamente.";
		next;
		mes "[Parmry Gianino]";
		mes "Quando você voltar, eu também irei coletar a taxa de ^5533FF50,000^000000 zeny, então não se esqueça disso também.";
		mes "Vejo você brevemente...";
		set JBLVL, JobLevel; // used to determine what item to get at the end
		set ALCH_Q,1;
		close;
L_LowLvl:
	mes "Você deve ao menos estar no Nível de Classe 40 para virar um Alquimista.";
	close;

L_Check:
	mes "Oh, você voltou... vamos ver...";
	next;
	mes "[Parmry Gianino]";
	if(countitem(612) < 100) goto L_NoItems;
	if(countitem(1005) && countitem(1006)) goto L_Skip;
	if(zeny < 50000) goto L_NoZeny;
 	set zeny,zeny - 50000;
	delitem 612, 100;
	mes "Você possui os 50,000 zeny...... Você também trouxe as 100 Mini-Fornalhas... Maravilhoso!";
	mes "Agora você está pronto para aprender o básico de ser um Alquimista.";
	next;

	L_Cont:
	mes "[Parmry Gianino]";
	mes "Porém antes disso... ^5533FFRasputin^000000 quer ver você...";
	mes "Hmm... Não tenho certeza do que poderia ser...";
	next;
	mes "[Parmry Gianino]";
	mes "Você deve visitar ele agora.  Somente desça as escadas a minha direita.  Sua sala fica no canto mais a sudeste.";
	set ALCH_Q,2;
	close;

	L_NoZeny:
		mes "Você ainda precisa pagar a taxa de ^FF553350,000^000000 zeny.";
		mes "Diga-me quando você tiver dinheiro o suficiente.";
		close;
	L_NoItems:
		mes "Como já foi mencionado anteriormente, você deve trazer ^FF3355100 Mini-Fornalhas^000000 para se juntar a União.";
		mes "Por favor, volte quando tiver conseguido as Mini-Fornalhas...";
		close;
L_Skip:
	mes "Oh, vejo que você tem um ^5533FFGrimório Antigo^000000 e um ^5533FFMartelo de Ferreiro^000000.....";
	mes "Isto significa que você não tem que pagar a taxa de entrada.";
	next;
	mes "[Parmry Gianino]";
	mes "Excelente.  Agora somente me dê o Livro, Martelo e as 100 Mini-Fornalhas. Assim você estará pronto para aprender o básico sobre ser um Alquimista.";
	next;
	delitem 1005,1;
	delitem 1006,1;
 	delitem 612, 100;
	goto L_Cont;

L_Test2:	
	mes "Vá visitar ^5533FFRasputin^000000 agora!";
	close;
L_Test3:	
	mes "Vá visitar ^5533FFDarwin^000000 agora!";
	close;
L_Test4:	
	mes "Vá visitar ^5533FFVan Helmont^000000 agora!";
	close;
L_Test5:	
	mes "Vá visitar ^5533FFNicholas Flamel^000000 agora!";
	close;
L_Test6:	
	mes "Volte para o ^5533FFNicholas Flamel^000000 agora!";
	mes "Ele ainda tem preparado mais um teste para você.";
	close;
L_GoChange:	
	mes "Nossa, você acabou todos estes testes? Perfeito! Agora você pode visitar o líder, ^5533FFVincent Carsciallo^000000.";
	mes "Ele é o que te transformará em um Alquimista.";
	close;
}


//==================================================================================================//
//					2nd Test: Math test :(
//==================================================================================================//
alde_alche,175,107,4	script	Rasputin Gregory	749,{
	mes "[Rasputin Gregory]";
	if(baseJob == Job_Merchant) goto L_Merc;
	mes "Que é isso, kikikiki.  Você está curiosos sobre o que estou fazendo...?";
	mes "Eu somente falarei especialmente para você. Veja, eu... Kekeke..";
	next;
	mes "[Rasputin Gregory]";
	mes "Estou pesquisando! Uma vez esta poção for completada... você será capaz de dominar um país inteiro!!";
	next;
	mes "[Rasputin Gregory]";
	mes "Kikikikikekekekek.  É um segredo para as pessoas de Prontera.....";
	close;
L_Merc:
	if(ALCH_Q == 2) goto L_Start;
	if(ALCH_Q == 3) goto L_Done;
	mes "Kikikikikekekekeke.";
	mes "O que é?! Seu inútil!";
	next;
	mes "[Rasputin Gregory]";
	mes "Um Mercador poderia ir embora e fazer seu comércio...";
	mes "Porque você viria em um local como este!?";
	mes "Você está procurando por algo para vender?";
	next;
	mes "[Rasputin Gregory]";
	mes "Kikiki... saía, vá embora daqui.";
	mes "Não desperdice tempo... Kekeke.";
	close;
L_Start:
	if(ALCH_Q2 == 1) goto L_ReTest;
	if(JobLevel == 50) goto L_Skip;
	mes "Keke, outro idiota que falhou aqui dentro.";
	mes "Juntar-se a União?";
	next;
	mes "[Rasputin Gregory]";
	mes "Eu não gostei... Eu NÃO gostei...!";
	mes "Todo lugar, todos se juntam a União e são chamados de Alquimista somente por saberem como misturar ervas!!!";
	next;
	mes "[Rasputin Gregory]";
	mes "Kikiki, então por esse motivo uma entrevista é necessária!";
	mes "Eu planejo castigar todos os incompetentes e persegui-los!!";
	next;
	mes "[Rasputin Gregory]";
	mes "Você está com uma cara de estúpido. Deve estar sendo pouco doido.";
	mes "Se você estava pensando que era somente mudar suas roupas, você pensou errado... Kekeke...";
	next;
	mes "[Rasputin Gregory]";
	mes "Agora tente resolver estes problemas que eu der a você.";
	mes "Vamos ver o quão esperto você é.";
	next;
	set @score,0;

	//set of question 1
	q1:
		mes "[Rasputin Gregory]";
		mes "12+23+34+45 =?";
		next;
		input @num;
		if(@num == 114) set @score,@score+10;
	q2:	
		mes "[Rasputin Gregory]";
		mes "1000-36-227-348 =?";
		next;
		input @num;
		if(@num == 389) set @score,@score+10;
	q3:
		mes "[Rasputin Gregory]";
		mes "9765/3/5/7 =?";
		next;
		input @num;
		if(@num == 93) set @score,@score+10; 
	q4:
		mes "[Rasputin Gregory]";
		mes "(2646/7) + (13*28) =?";
		next;
		input @num;
		if(@num == 742) set @score,@score+10;
	q5:
		mes "[Rasputin Gregory]";
		mes "Com um desconto de 24%";
		mes "quanto vale 12 Poções Vermelhas,";
		mes "5 Asas de Mosca , e ";
		mes "1 Asa de Borboleta em zenys?";
		next;
		input @num;
		if(@num == 909) set @score,@score+10;
	q6:
		mes "[Rasputin Gregory]";
		mes "Qual o peso total de 3 ";
		mes "Cimitarras,";
		mes "2 Elmos e 1 Casaco?";
		next;
		input @num;
		if(@num == 450) set @score,@score+10;
	q7:	
		mes "[Rasputin Gregory]";
		mes "Qual a defesa total de";
		mes "uma Solidéu, um Capote, uma Máscara do Fantasma,";
		mes "um Laço, um Sobrepeliz,";
		mes "um par de Botas, e um Aquecedor de Orelha?";
		next;
		input @num;
		if(@num == 19) set @score,@score+10;
	q8:	
		mes "[Rasputin Gregory]";
		mes "Se você comprar 5 Elmos com 24%";
		mes "de desconto e vender eles a 20%, quanto você";
		mes "ganharia em zenys?";
		next;
		input @num;
		if(@num == 8800) set @score,@score+10;
		goto L_Total;

L_ReTest:
	mes "O que? Você quer refazer o teste?";
	mes "Eu achei que tivesse mandado você ir embora.";
	next;
	mes "[Rasputin Gregory]";
	mes "Bem, eu irei permitir você tentar mais uma vez...";
	mes "Eu perdoo você... Kekeke.";
	mes "Faça direito desta vez...";
	next;
	mes "[Rasputin Gregory]";
	mes "Agora então, responda as minhas perguntas.";
	mes "Vamos ver o quão esperto você é.";
	next;
	set @score,0;

	//set of question 2
	q11:
		mes "[Rasputin Gregory]";
		mes "13+25+37+48=?";
		next;
		input @num;
		if(@num == 123) set @score,@score+10;
	q12:
		mes "[Rasputin Gregory]";
		mes "1000-58-214-416 =?";
		next;
		input @num;
		if(@num == 312) set @score,@score+10;
	q13:
		mes "[Rasputin Gregory]";
		mes "12*24*3=?";
		next;
		input @num;
		if(@num == 864) set @score,@score+10;
	q14:
		mes "[Rasputin Gregory]";
		mes "10530/3/5/2=?";
		next;
		input @num;
		if(@num == 351) set @score,@score+10;
	q15:
		mes "[Rasputin Gregory]";
		mes "(35*19) - (1792/7) =?";
		next;
		input @num;
		if(@num == 409) set @score,@score+10;
	q16:
		mes "[Rasputin Gregory]";
		mes "(2368/8) + (24*17) = ?";
		next;
		input @num;
		if(@num == 704) set @score,@score+10;
	q17:
		mes "[Rasputin Gregory]";
		mes "(2646/7) + (13*28)=?";
		next;
		input @num;
		if(@num == 742) set @score,@score+10;
	q18:
		mes "[Rasputin Gregory]";
		mes "Se comprando estes itens com 24% de";
		mes "desconto, qual o preço total de";
		mes "15 Poções Verdes,";
		mes "6 Lupas e 4 Armadilhas?";
		next;
		input @num;
		if(@num == 934) set @score,@score+10;
	q19:
		mes "[Rasputin Gregory]";
		mes "Qual o peso total de";
		mes "3 Sabres de Impacto,";
		mes "4 Barretes e 2 pares de Botas?";
		next;
		input @num;
		if(@num == 550) set @score,@score+10;
	q20:
		mes "[Rasputin Gregory]";
		mes "Qual é o total de defesa de um";
		mes "Broquel, um Casaco, uma Máscara de Gás,";
		mes "um Laço Grande, um Laço, um Chapéu Chinês e";
		mes "um Óculos?";
		next;
		input @num;
		if(@num == 16) set @score,@score+10;
	q21:
		mes "[Rasputin Gregory]";
		mes "Quantos zeny você tem de lucro";
		mes "se você comprar uma Malha com 24%";
		mes "de desconto";
		mes "e vender a mesma com 20% do preço normal?";
		next;
		input @num;
		if(@num == 2840) set @score,@score+10;
L_Total:
	mes "[Rasputin Gregory]";
	if(@score < 80) goto L_Failed;
	set ALCH_Q,3;
	set ALCH_Q2, 0;
	mes "Kikiki... Eu deixarei você ir.";
	mes "Faça algumas boas pesquisas e seja de alguma ajuda para o Rasputin... Kikiki!";
	next;	
	mes "[Rasputin Gregory]";
	mes "Bem, agora vá! ^5533FFVá até Darwin^000000.";
	mes "Ele te ensinará como fazer os experimentos.";
	mes "Diga a ele que eu te enviei.";
	next;
	mes "[Rasputin Gregory]";
	mes "Kikikikekeke.";
	mes "Não pense que já terminou!";
	close;

	L_Failed:
		set ALCH_Q2,1;
		mes "...Kekeke, seu idiota!";
		mes "Eu me senti um idiota fazendo todas essas perguntas a você!";
		mes "VOCÊ ERROU TODAS ELAS!!!";
		next;
		mes "[Rasputin Gregory]";
		mes "Como pode uma pessoa que nem ao menos respondeu uma destas perguntas PENSAR em se tornar um Alquimista!?";
		mes "Que? Você fez alguma certa?";
		next;
		mes "[Rasputin Gregory]";
		mes "Estúpido! Se você ao menos errar uma, todas estarão erradas para um Alquimista!";
		mes "Saia! Deixe-me! Vá embora daqui!";
		close;
L_Done:
	mes "O que você está fazendo? Saia daqui.";
	mes "Vá ver ^5533FFDarwin^000000.";
	mes "Ele irá te ensinar como fazer os experimentos.";
	mes "Diga a ele que eu te enviei.";
	next;
	mes "[Rasputin Gregory]";
	mes "Kikikikekeke.";
	mes "Não pense que já terminou!";
	close;
L_Skip:
	mes "Hmm... você tem um nível de classe bem alto... Eu irei ser legal hoje e deixar você passar.";
	mes "Você agora vá ver o ^5533FFDarwin^000000.";
	mes "Ele irá te ensinar como fazer os experimentos.";
	mes "Diga a ele que eu te enviei.";
	set ALCH_Q,3;
	set ALCH_Q2, 0;
	close;
}


//==================================================================================================//
//					3rd Test: Mix some medicine
//==================================================================================================//
alde_alche,13,15,8	script	Darwin	750,{
	mes "[Darwin]";
	if(baseJob == Job_Merchant) goto L_Merc;
L_Other:
	mes "Mas para perder ou ganhar...";
	mes "É sempre assim...";
	next;
	mes "[Darwin]";
	mes "Tudo que eu posso ver com meus olhos amaldiçoados";
	mes "são ilusões do passado,";
	mes "Ahahahaha...";
	next;
	mes "[Darwin]";
	mes "Não há coisas como o paraíso... há?";
	mes "Minha amada Harmona.... Aah....";
	close;

L_Merc:
	if(ALCH_Q == 3) goto L_Start;
	if(ALCH_Q == 4) goto L_Done;
	goto L_Other;

L_Start:
	if(ALCH_Q2 == 1) goto L_Check;
	mes "...........";
	mes ".........";
	mes "....Quem é esse...";
	next;
	mes "[Darwin]";
	mes "Um lobo? Ou um humano...";
	mes "Você deve estar procurando por algo também.";
	next;
	mes "[Darwin]";
	mes "Se você tem algo precioso, tenha cuidado.";
	mes "Você pode perder alguma coisa a mais enquanto estiver atrás de outra...";
	next;
	mes "[Darwin]";
	mes "..........";
	next;
	mes "[Darwin]";
	mes "...mas o que trás você aqui?";
	mes "Vindo em um lugar como esse?";
	next;
	menu "Eu quero aprender como fazer experimentos.",M_Exp, "Diga-me mais sobre as flores.",M_Flow;

	M_Flow:	
		mes "[Darwin]";
		mes ".......";
		mes "......";
		mes "...pense sobre suas coisas preciosas...";
		next;
		mes "[Darwin]";
		mes ".... Há uma flor que eu vagamente me lembro....";
		next;
		mes "[Darwin]";
		mes "Eu me envolvi pesquisando uma coisa...";
		mes "Para a única que eu amo...";
		next;
		mes "[Darwin]";
		mes "Vamos dizer que era sobre um relacionamento entre lobo e flores...";
		mes "Eu não direi você os detalhes.";
		next;
		mes "[Darwin]";
		mes "Mas sim... era uma flor.";
		mes "Com seu brilho, assim mesmo como diziam, a paradisíaca Flor da Ilusão."; // with its shine, it was said to  let you see paradise Illusion Flower...
		next;
		mes "[Darwin]";
		mes "Eu fiz um homúnculo.";
		mes "Ninguém acreditou em mim. Eu criei vida de uma flor!!!";
		mes "Com minhas pesquisas!";
		next;
		mes "[Darwin]";
		mes "Mas agora, nada sobrou...";
		mes "Tudo está terminado.";
		mes "A minha vida parou naquele instante..."; //Time has stopped at that moment in my life...
		next;
		mes "[Darwin]";
		mes "Aargh... Harmona, nos lindos campos de flores,";
		mes "Aonde você foi parar...";
		close;
	M_Exp:
		mes "[Darwin]";
		mes "Você quer aprender alquimia...";
		mes "Todo conhecimento que eu possuo...";
		mes "Eu tive que tornar meus sonhos verdadeiros..."; //I made to make my dreams come true...
		next;
		mes "[Darwin]";
		mes "Eu ensinarei você o básico...";
		mes "Mas você tem que determinar seus próprios desejos.";
		next;
		mes "[Darwin]";
		mes "Então, aqui vai o básico.";
		mes "Isto é como se faz um remédio simples...";
		mes "Prepare os ingredientes...";
		next;
		mes "[Darwin]";
		mes "^5533FF3 Vasilhas de Mistura";
		mes "3 Garrafas Vazias";
		mes "1 Erva Vermelha";
		mes "1 Erva Amarela";
		mes "1 Erva Branca^000000";
		next;
		mes "[Darwin]";
		mes "Depois que você providenciar todos esses... venha aqui.";
		set ALCH_Q2, 1;
		close;

L_Check:
	mes "...........";
	mes ".........";
	mes "...Quem é...?";
	next;
	mes "[Darwin]";
	mes "Ah, você é aquele que queria aprender sobre alquimia...";
	mes "Então você providenciou os itens?";
	next;
	if(countitem(7134) < 3 || countitem(713) < 3 || countitem(507) < 1 || countitem(508) < 1 || countitem(509) < 1) goto L_NotEnuf;
	delitem 7134,3;
	delitem 713,3;
	delitem 507,1;
	delitem 508,1;
	delitem 509,1;
	mes "[Darwin]";
	mes "Parece que você já tem tudo pronto.";
	mes "Como prometi, ensinar-lhe-ei como fazer o remédio.";
	next;
	mes "[Darwin]";
	mes "Primeiramente, prepare a Vasilha de Mistura e coloque as ervas em cima, fazendo assim.";
	mes "E você, com cuidado, esmaga elas.";
	next;
	mes "[Darwin]";
	mes "Coloque poucas quantidades de água limpa e misture até ela ficar grossa,";
	mes "então você adicionar mais ervas.";
	next;
	mes "[Darwin]";
	mes "É assim que se faz. Quando você achar que já tem o suficiente de ervas,";
	mes "gentilmente coloque-o na Garrafa Vazia.";
	next;
	mes "[Darwin]";
	mes "Aqui está, ficou completa.";
	mes "Dessa vez, faça como eu mostrei a você.";
	mes "Deverá ser fácil porque é realmente simples.";
	next;
	mes "[Darwin]";
	mes "...mesmo assim, porque não tenta fazer um?"; //even though you don't have one?
	next;
	set @score,0;
	menu "Preparar a Vasilha de Mistura",-, "Vestir a Vasilha de Mistura na cabeça.",M_0, "Chutar a Vasilhas de Mistura.",M_0;

		set @score,@score+10;
	M_0:

	menu "Colocar areia na Vasilha de Mistura",M_1, "Colocar ervas na Vasilha de Mistura",-, "Colocar a harpa dentro da Vasilha de Mistura",M_1;

		set @score,@score+10;
	M_1:

	menu "Esmagar as ervas.",-, "Esmagar a Vasilha de Mistura.",M_2, "Esmagar os pés de Darwin",M_2; //Smash the Tarosia's foots

		set @score,@score+10;
	M_2:

	menu "Gotear a água.",M_3, "Beber a água.",M_3, "Colocar poucas quantidades de água.",-; //Pour in the water

		set @score,@score+10;
	M_3:

	menu "Continuar esmagando as ervas.",-, "Continuar comendo as ervas.",M_4, "Continuar dançando e cantando.",M_4;

		set @score,@score+10;
	M_4:

	menu "Colocar na panela e fritar.",M_5, "Colocar na Garrafa Vazia.",-, "Levantar a Vasilha de Mistura e beber.",M_5; //Put in the noodles and fried it

		set @score,@score+10;
	M_5:

	mes "[Darwin]";
	if(@score < 60) goto L_Failed;
	mes "... Bom trabalho. Ficou muito bom considerando que é sua primeira vez...";
	mes "Como uma lembrança, pegue isso.";
	getitem 501,1;
	getitem 503,1;
	getitem 504,1;
	set ALCH_Q,4;
	set ALCH_Q2,0;
	next;
	mes "[Darwin]";
L_Done:
	mes "Vá até ^5533FFVan Helmont^000000 na próxima sala...";
	mes "Ele te ensinará a você mais detalhes.";
	mes "Vá aprender com ele.";
	next;
	mes "[Darwin]";
	mes "Não se esqueça...";
	mes "Você deve proteger o que é realmente precioso para você...";
	close;

	L_Failed:
		mes "... Você estragou todos os ingredientes.";
		mes "... Vá buscar mais alguns.";
		close;
L_NotEnuf:
	mes "[Darwin]";
	mes "...Você deve ter esquecido de quais ingredientes trazer.";
	mes "Vou dizer de novo.";
	mes "Não os esqueça dessa vez.";
	next;
	mes "[Darwin]";
	mes "^5533FF3 Vasilhas de Mistura";
	mes "3 Garrafas Vazias";
	mes "1 Erva Vermelha";
	mes "1 Erva Amarela";
	mes "1 Erva Branca^000000";
	next;
	mes "[Darwin]";
	mes "Volte quando você estiver pronto...";
	close;	
}


//==================================================================================================//
//					4th Test: Learn from Morgenstein
//==================================================================================================//
alde_alche,79,19,4	script	Van Helmont	748,{
	mes "[Van Helmont]";
	if(baseJob == Job_Merchant) goto L_Merc;
L_Other:
	mes "Somente mais um pouco... sim, só mais um pouquinho...";
	mes "ARGH! Somente mais um pouco e eu poderia ter feito!";
	next;
	mes "[Van Helmont]";
	mes "Porque, porque outra falha!?!?!?";
	mes "Nada estava errado!";
	mes "PORQUE! PORQUE!";
	next;
	mes "[Van Helmont]";
	mes "Isto significa que eu terei que conseguir mais ingredientes...";
	mes "Mas eu deveria levar um dia inteiro para conseguir eles..."; //But I should be able to complete it one day....Heeh...
	close;
L_Merc:
	if(ALCH_Q == 4) goto L_Start;
	if(ALCH_Q == 5) goto L_Done;
	goto L_Other;
L_Start:
	if(ALCH_Q2 == 1) goto L_Check;
	mes "Arrrrgh... Porque esta formula não continua...? O que há de errado?"; //Why does this formula just stop... What's wrong.
	mes "Teoricamente está tudo correto...";
	mes "Tenho certeza que é algum erro na fórmula o motivo disso...";
	mes "Em algum lugar...";
	next;
	mes "[Van Helmont]";
	mes "É isto, eu despejo aqui e poderia funcionar... Mas aqui na solução da formula não faz nenhum sentindo..."; //That's it, I pour it here and it should stop... but this is the formula that solution doesn't stop flowing.
	mes "Como eu pude fazer tal erro... Quando foi que eu inverti esses...";
	next;
	mes "[Van Helmont]";
	mes "Sim, achei. Eu preciso consertar esta parte.";
	mes "Não há necessidade de começar de novo.";
	mes "Eu somente tenho que consertar...";
	mes "....Mas, espera, espera.";
	next;
	mes "[Van Helmont]";
	mes ".....................";
	next;
	mes "[Van Helmont]";
	mes "....Quem é você?";
	next;
	menu "Eu quero me tornar um Alquimista.",-,".....",M_Bye;

		mes "[Van Helmont]";
		mes "Oh, entendo....";
		mes "Hahahaha, você quer aprender alquimia em um mundo como esse, que Mercador engraçado.... hmmm.";
		next;
		mes "[Van Helmont]";
		mes "Bem, isso é bom, mas eu tenho um experimento urgente que eu devo fazer.";
		mes "Então não fique no caminho.";
		next;
		menu "Ensine me alguma coisa.",-,"..........",M_Bye;

			mes "[Van Helmont]";
			mes "Argh... eu não disse para não me atrapalhar?";
			mes "Porque você não consigue entender quando eu falo algo?!";
			next;
			mes "[Van Helmont]";
			mes "Tudo bem, darei você uma tarefa.";
			mes "Vá estudar e volte. Vamos ver...";
			mes "O que poderia ser bom... Hmm...";
			next;
			mes "[Van Helmont]";
			mes "Oh, já sei. Vá aprender do ^5533FFMorgenstein^000000. Aprenda como fazer um ^009500Antígeno^000000 e uma ^FF3355Mistura^000000.";
			mes "Você pode encontrar ele em ^5533FFGeffen^000000.";
			next;
			mes "[Van Helmont]";
			mes "Você não precisa trazer o que você fizer. Mas só vá assistir como ele faz os remédios."; // You don't need to bring what you made. But just go watch how he makes medicine and stuff.
			mes "Entendeu?";
			next;
			mes "[Van Helmont]";
			mes "Bem então, vejo você mais tarde.";
			mes "Você deveria ir o mais brevemente possível.";
			set ALCH_Q2,1;
			close;
	M_Bye:
		mes "[Van Helmont]";
		mes "Se você não tem negócios aqui, vá embora! Já!!!";
		close;
L_Check:	
	if(al_morgen != 1) goto L_NotRdy;
	mes "Sim, cortando o Tentáculo";
	mes "e misturando alguns Mucos Pegajosos,";
	mes "A solução de Jellopys no tubo de testes... Droga, aonde a Vasilha de Mistura foi parar?";
	next;
	mes "[Van Helmont]";
	mes "Será que eu usei todas elas? Em uma hora como esse... Grrrrr. Creio que Nicolas tem algumas sobrando.";
	mes "Ganfunnit... que droga.";
	mes "....mas, espera, espera.";
	next;
	mes "[Van Helmont]";
	mes "Ah, você é o Mercador de antes. Então, aprendeu algo?";
	mes "Eu não mandei você lá para brincar.";
	next;
	mes "[Van Helmont]";
	mes "Deixe-me perguntar a você algumas perguntas. Responda com o que você aprendeu.";
	next;
	set @score,0;

	Q_1a:
		mes "[Van Helmont]";
		mes "Que item é necessário para produzir o Antígeno?";
		next;
		menu "Pluma",-,"Muco Pegajoso",-,"Sangue de Animal",-;

	Q_1b:
		mes "[Van Helmont]";
		mes "Quais desses itens não é necessário para a formação do Antígeno?";
		next;
		menu "Karvodailnirol",-,"Detrimindexta",Q_2a,"Álcool",Q_2a;

			set @score,@score+10;

	Q_2a:
		mes "[Van Helmont]";
		mes "Que item é necessário para fazer uma Mistura?";
		next;
		menu "Ração para Monstros",-,"Lábios Fossilizados",-,"Bandagem Estragada",-;

	Q_2b:
		mes "[Van Helmont]";
		mes "Qual desses itens não é necessário no processo para fazer uma Mistura?";
		next;
		menu "Karvodailnirol",L_Result,"Detrimindexta",-,"Álcool",L_Result;

			set @score,@score+10;

L_Result:
	mes "[Van Helmont]";
	if(@score < 20) goto L_Failed;
	mes "Hmmm, você aprendeu direito. Tudo bem, agora você sabe algo sobre remédios?";
	mes "Ahn?";
	mes "Eu sei, você deve ter muito o que dizer mas deixe-me continuar com meu experimento.";
	next;
	mes "[Van Helmont]";
	mes "Saia e vá até o próximo aposento.  ^5533FFNicholas^000000 provavelmente tem mais o que ensinar a você do que eu.";
	set ALCH_Q,5;
	set ALCH_Q2,0;
	close;

	L_Failed:
		mes "...Seja sincero. Você não sabe,";
		mes "estou errado? Eu disse para você aprender direito com o Morgenstein...";
		next;
		mes "[Van Helmont]";
		mes "Nem sequer pense em voltar antes de ter aprendido tudo com certeza!";
		mes "Pare de me atrapalhar e vá embora!";
		close;
L_NotRdy:
	mes "Eu não disse a você para visitar Morgenstein em Geffen!?!";
	mes "Ele é um homem sábio! Você deveria falar com ele!";
	close;
L_Done:
	mes "O que você está fazendo? Saia e vá até o próximo aposento.  ^5533FFNicholas^000000 provavelmente tem mais o que te ensinar do que eu.";
	mes "Eu não tenho mais negócios a tratar com você.";
	close;

}


//==================================================================================================//
//				Test 5 & 6: Word Scramble, Help the Baijin Bros.
//==================================================================================================//
alde_alche,145,19,4	script	Nicholas Flamel	57,{
	mes "[Nicholas Flamel]";
	if(baseJob == Job_Merchant) goto L_Merc;
L_Other:
	mes "Lorem ipsum dolor sit amet,";
	mes "consectetuer adipiscing elit.";
	mes "Vivamus sem. Sed metus lacus, viverra id, rutrum eget, rhoncus sit amet, lectus.";
	mes "Suspendisse sit amet urna in nisl frigilla faucibus.";
	mes "Nulla scelerisque eros....";
	mes "......resmungandoresmungando........";
	close;
L_Merc:
	if(ALCH_Q == 5) goto L_Start;
	if(ALCH_Q == 6) goto L_Start2;
	if(ALCH_Q == 7) goto L_Done;
	goto L_Other;
L_Start:
	if(ALCH_Q2 == 1) goto L_ReTest;
	mes "Ooh... você é o novo Mercador que quer se tornar um Alquimista?";
	mes "Mas você sabe... não todo mundo pode ser tornar um.";
	next;
	mes "[Nicholas Flamel]";
	mes "A União não gosta daqueles que somente viram Alquimista sem um objetivo bem nítido.";
	mes "Então... para ver se você é capacitado, eu vou testar o quão inteligente você é.";
	next;
	mes "[Nicholas Flamel]";
	mes "Como você sabe, Alquimistas devem memoriza muitas equações,";	
	mes "equações químicas e muitas outras informações.";
	mes "Se você não é de um certo nível,";
	mes "vai ser bem complicado."; //it's actually pretty tough.
	next;
	mes "[Nicholas Flamel]";
	mes "A idéia é previnir que Alquimistas desnecessários se juntem a União!";
	next;
	mes "[Nicholas Flamel]";
	mes "Certo... Aqui vão as perguntas!";
	mes "Isto é para testar sua concentração então não fique confuso enquanto estiver olhando para os gráficos.";
	next;
	mes "[Nicholas Flamel]";
	mes "Encontre as palavras em um grupo de letras. As palavras têm que ser feitas usando todas ou algumas letras disponíveis que lhe são dados.";
	next;
	mes "[Nicholas Flamel]";
	mes "Você passa se a palavra estive 'Dentro' do quebra-cabeça.";
	next;

	Q_1:
		set @score,0;
		mes "[Nicholas Flamel]";
		mes "s m i e x b w u n e n t a g l r";
		next;
		menu "tigre",-,"lobo",Q_2,"abobora",Q_2,"tropeçar",Q_2;

			set @score,@score+10; 

	Q_2:
		mes "[Nicholas Flamel]";
		mes "n i e g b o p r c o a u w r v";
		next;
		menu "golpe",Q_3,"provocar",Q_3,"vigor",-,"choque",Q_3,"abracadabra",Q_3;

			set @score,@score+10; 

	Q_3:
		mes "[Nicholas Flamel]";
		mes "l r m r l f e c t a v i n e t d e o";
		next;
		menu "alberta",Q_4,"latifoliate",-,"carmesim",Q_4,"borbo",Q_4,"sempre-verde",Q_4;

			set @score,@score+10; 

	Q_4:
		mes "[Nicholas Flamel] ";
		mes "r o e h n ç r o f m c a i n d p t ã t";
		next;
		menu "forjadormercador",L_Score,"poçãomercador",-,"descmercador",L_Score,"vendedormercador",L_Score;
	
			set @score,@score+10; 
L_Score:
	if(@score < 40) goto L_Failed;
	set ALCH_Q,6;
	set ALCH_Q2,0;
	mes "[Nicholas Flamel]";
	mes "Você parece que terminou.";
	next;
	mes "[Nicholas Flamel]";
	mes "Vamos ver.";
	mes "...Bom trabalho. Excelente.";
	next;
	mes "[Nicholas Flamel]";
	mes "De alguma maneira você encontrou todas.";
	mes "Com este tipo de concentração, você poderia facilmente estar apto a ler os dicionários dos remédios.";
	mes "Vamos ver... o próximo é...";
	next;
	mes "[Nicholas Flamel]";
	mes "Volte daqui a pouco.";
	close;

	L_Failed:
		mes "[Nicholas Flamel]";
		mes "Hmmm... Você falhou!";
		mes "Pegue seus óculos, talvez você consiga passar da próxima vez usando eles.";
		set ALCH_Q2,1;
		close;
L_ReTest:
	mes "Hmmm, você voltou.";
	mes "Bem, veremos se você mudou..."; 
	next;
	goto Q_1;	

L_Start2:
	if(ALCH_Q2 > 0) goto L_Check2;
	mes "Próximo, você ter que ir a um lugar.";
	mes "Vá até ^5533FF'A Cidade Esquecida de Yuno'^000000 e encontre os irmãos ^FF3355Bain e Bajin^000000 que estão pesquisando alquimia com os sábios.";
	next;
	mes "[Nicholas Flamel]";
	mes "Vá ajudá-los e volte aqui. Pegue isto... vai ajudar você."; //it will help you get close to them
	next;
	getitem 974,1;
	getitem 7068,5;
	getitem 7043,5;
	getitem 756,3;
	getitem 757,3;
	mes "[Nicholas Flamel]";
	mes "1 Mistura";
	mes "5 Madeira Queimada";
	mes "5 Areia Fina";
	mes "3 Minério de Oridecon";
	mes "3 Minério de Elunium";
	set ALCH_Q2,1;
	next;
	mes "[Nicholas Flamel]";
	mes "Tenha uma viagem segura.";
	mes "É Yuno. Venha em um piscar de olhos."; //Come back in one piece.
	close;
L_Check2:
	if(ALCH_Q2 < 2) goto L_NotDone;
	set ALCH_Q, 7;
	set ALCH_Q2, 0;
	mes "Ooh! Muito bom ao ter vindo dessa longa viagem."; //Well done on such a long  trip.
	mes "Eu recebi a mensagem dizendo que os irmãos Bain e Bajin ficarão felizes com sua visita.";
	next;
	mes "[Nicholas Flamel]";
	mes "Você tem que suceder em os ajudar.";
	mes "Se você for bom o suficiente em ajudar os irmãos... tudo bem, eu acho que você será aceito.";
	next;
	mes "[Nicholas Flamel]";
L_Done:
	mes "Vá até o Líder da União no segundo andar!";
	mes "Parabéns. Você irá mudar de classe em breve!";
	close;

	L_NotDone:
		mes "Os irmãos Bain e Bajin estão em Yuno esperando sua ajuda.";
		close;

}


//==================================================================================================//
//					Job Changer
//==================================================================================================//
alde_alche,101,184,4	script	Vincent Carsciallo	122,{
	mes "[Vincent Carsciallo]";
	if(baseJob == Job_Merchant) goto L_Merc;
	if(baseJob == Job_Alchem){
		mes "Como a alquimia prosegue? Tenha certeza em ser cuidadoso e pensar sobre segurança em todos os momentos.";
		mes "Eu não gostaria que você se explodisse ou qualquer coisa assim...";
		close;
	}
	mes "Hmmm? O que um cidadão faria em um local como este?";
	next;
	mes "[Vincent Carsciallo]";
	mes "Não há nada do que se divertir. Não fique vagando e saia daqui.";
	close;
L_Merc:
	if(ALCH_Q == 7) goto L_Change;
	mes "Hmmm? Um Mercador.";
	mes "Você está interresado em alquimia?";
	next;
	mes "[Vincent Carsciallo]";
	mes "Esta é a União Alquimista.";
	mes "Nós pesquisamos muitas substâncias diferentes.";
	mes "Nosso objetivo é fazer alguma coisa nova sem o poder da magia.";
	next;
	mes "[Vincent Carsciallo]";
	mes "Criar nova vida, e estudar como vive mais e não morrer...";
	mes "Nós também fazemos novos materiais a partir dos que já existem...";
	mes "Estas são algumas das coisas que fazemos.";
	next;
	mes "[Vincent Carsciallo]";
	mes "Depois de ser um Mercador por um longo tempo";
	mes "você começa a se tornar interresado em tudo.";
	mes "Se você gosta de aprender alquimia também,";
	mes "tente fazer parte da nossa União.";
	close;
L_Change:
	if(skillpoint){
		mes "Por favor, use todos seus pontos de habilidades antes de falar comigo.";
		close;	
	}
	mes "Ooh, certo. Você já junto-se e aprendeu o básico também. Muito bom.";
	next;
	set @gift,JBLVL;
	callfunc "Job_Change",Job_Alchem;
	callfunc "F_ClearJobVar";
	emotion e_grat;
	mes "[Vincent Carsciallo]";
	mes "De agora em diante, você também é um membro reconhecido da União dos Alquimistas.";
	mes "Eu acredito que você aprendeu bastante...";
	next;
	if(@gift==50)
		getitem 7133,1;
	else
		getitem 7127,1;
	mes "[Vincent Carsciallo]";
	mes "Aqui está uma pequena coisa.";
	mes "Use-o para começar com alguma pesquisa.";
	next;
	mes "[Vincent Carsciallo]";
	mes "Eu verei você mais tarde então...";
	mes "Tenha orgulho de ser um Alquimista!";
	close;

}
